import std.json (Json)
import std.net.http (Header, Status)
import std.net.http.client (Client)
import std.uri (Uri)

let URL = 'https://api.github.com/graphql'

fn get(
  client: mut Client,
  query: String,
  token: String,
  variables: Map[String, String],
) -> Result[Json, String] {
  let root = Map.new
  let vars = Map.new

  for (key, val) in variables { vars.set(key, Json.String(val)) }

  root.set('query', Json.String(query))
  root.set('variables', Json.Object(vars))

  let body = Json.Object(root).to_string
  let resp = match
    client
      .post(Uri.parse(URL).or_panic)
      .header(Header.content_type, 'application/json')
      .header(Header.authorization, 'bearer ${token}')
      .body(body)
  {
    case Ok(v) -> v
    case Error(e) -> throw 'failed to send the request: ${e}'
  }

  if resp.status != Status.ok {
    throw 'the server returned a ${resp.status} response'
  }

  Json.parse(resp.body).map_error(fn (e) { 'failed to parse the body: ${e}' })
}
